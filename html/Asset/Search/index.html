<%init>
my $assets = RT::Assets->new($session{CurrentUser});

for my $key (keys %ARGS) {
    next unless defined $ARGS{$key} and length $ARGS{$key};
    if ($key =~ /^(Name|Description)$/) {
        $assets->Limit(
            FIELD => $key,
            OPERATOR => 'LIKE',
            VALUE => $ARGS{$key},
        );
    } elsif ($key =~ /^Role\.(.+)/) {
        my $role = $1;
        $assets->RoleLimit(
            TYPE      => $role,
            FIELD     => $_,
            VALUE     => $ARGS{$key},
            SUBCLAUSE => $role,
            ENTRYAGGREGATOR => "OR",
            CASESENSITIVE   => 0,
        ) for qw/EmailAddress Name/;
    } elsif ($key =~ /^CF\.\{(.+?)\}$/ or $key =~ /^CF\.(.*)/) {
        my $cf = RT::Asset->new( $session{CurrentUser} )
            ->LoadCustomFieldByIdentifier( $1 );
        next unless $cf->id;
        $assets->LimitCustomField(
            CUSTOMFIELD => $cf->Id,
            OPERATOR    => "LIKE",
            VALUE       => $ARGS{$key},
        );
    }
}
$assets->LimitToActiveStatus;

my $Format = RT->Config->Get('AssetSearchFormat') || q[
    '<b><a href="__WebPath__/Asset/Display.html?id=__id__">__id__</a></b>/TITLE:#',
    '<b><a href="__WebPath__/Asset/Display.html?id=__id__">__Name__</a></b>/TITLE:Name',
    Description,
    Status,
];
</%init>
<& /Elements/Header, Title => loc("Assets") &>
<& /Elements/Tabs &>

<& /Elements/CollectionList,
    Collection      => $assets,
    OrderBy         => 'Name',
    Order           => 'ASC',
    Format          => $Format,
    AllowSorting    => 0,
    &>

<&|/Widgets/TitleBox, title => "Search Assets" &>
<form action="<% RT->Config->Get('WebPath') %>/Asset/Search/index.html" name="AssetSearch">

<table>
<tr><td class="label"><label for="Name"><&|/l&>Name</&></label></td>
    <td class="value"><input type="text" id="Name" name="Name" value="<% $ARGS{Name} || ''%>" /></td></tr>
<tr><td class="label"><label for="Description"><&|/l&>Description</&></label></td>
    <td class="value"><input type="text" id="Description" name="Description" value="<% $ARGS{Description} || ''%>" /></td></tr>

% for my $role (RT::Asset->Roles) {
<tr>
  <td class="label"><label for="Role.<% $role %>"><% loc($role) %></td>
  <td class="value">
      <input type="text" id="Role.<% $role %>" name="Role.<% $role %>"
             value="<% $ARGS{"Role.$role"} || '' %>" />
  </td>
</tr>
% }

% my $CFs = RT::CustomFields->new( $session{CurrentUser} );
% $CFs->LimitToChildType( RT::Asset->CustomFieldLookupType );
% while (my $cf = $CFs->Next) {
% my $name = "CF.{" . $cf->Name . "}";
<tr>
  <td class="label"><label for="<% $name %>"><% $cf->Name %></label></td>
  <td class="value">
% if ($cf->Type =~ /^Date(Time)?$/) {
<& /Elements/SelectDate, ShowTime => ($1 ? 1 : 0), Name => $name, Value => $ARGS{$name} || '' &>
% } else {
<& /Elements/SelectCustomFieldValue, CustomField => $cf, Name => $name, Default => $ARGS{$name} || '' &>
% }
% }

</table>

<& /Elements/Submit, Label => loc('Search') &>
</form>

</&>

