<%args>
$Order => "ASC"
$OrderBy => "Name"
</%args>
<%init>
my $asset_obj = RT::Asset->new($session{'CurrentUser'});
my $catalog_obj = LoadDefaultCatalog($ARGS{'Catalog'} || '');
$ARGS{'Catalog'} = $catalog_obj->Id;

my $assets = RT::Assets->new($session{CurrentUser});
my @PassArguments = qw( Order OrderBy );

if ( $ARGS{'SearchAssets'} ){
    for my $key (keys %ARGS) {
        my $value = ref $ARGS{$key} ? $ARGS{$key}[0] : $ARGS{$key};
        next unless defined $value and length $value;
        if ($key =~ /^(Name|Description)$/) {
            $assets->Limit(
                FIELD => $key,
                OPERATOR => 'LIKE',
                VALUE => $value,
            );
        } elsif ($key =~ /^(Catalog|Status)$/) {
            $assets->Limit(
                FIELD => $key,
                VALUE => $value,
            );
        } elsif ($key =~ /^Role\.(.+)/) {
            my $role = $1;
            $assets->RoleLimit(
                TYPE      => $role,
                FIELD     => $_,
                VALUE     => $value,
                SUBCLAUSE => $role,
                ENTRYAGGREGATOR => "OR",
                CASESENSITIVE   => 0,
            ) for qw/EmailAddress Name/;
        } elsif ($key =~ /^CF\.\{(.+?)\}$/ or $key =~ /^CF\.(.*)/) {
            my $cf = RT::Asset->new( $session{CurrentUser} )
              ->LoadCustomFieldByIdentifier( $1 );
            next unless $cf->id;
            $assets->LimitCustomField(
                CUSTOMFIELD => $cf->Id,
                OPERATOR    => "LIKE",
                VALUE       => $value,
            );
        }
        push @PassArguments, $key;
    }
}

my $Format = RT->Config->Get('AssetSearchFormat') || q[
    '<b><a href="__WebPath__/Asset/Display.html?id=__id__">__id__</a></b>/TITLE:#',
    '<b><a href="__WebPath__/Asset/Display.html?id=__id__">__Name__</a></b>/TITLE:Name',
    Description,
    Status,
];
</%init>
<& /Elements/Header, Title => loc("Assets") &>
<& /Elements/Tabs &>

% if ( $ARGS{'SearchAssets'} ){
<& /Elements/CollectionList,
    (map {+($_ => $ARGS{$_})} @PassArguments),
    Collection      => $assets,
    OrderBy         => $OrderBy,
    Order           => $Order,
    Format          => $Format,
    AllowSorting    => 1,
    PassArguments   => \@PassArguments,
    &>
%}
<form action="<% RT->Config->Get('WebPath') %>/Asset/Search/index.html" name="AssetSearch">
<&| /Widgets/TitleBox, title => "Search Assets" &>
<& /Asset/Elements/AssetSearchBasics, %ARGS, CatalogObj => $catalog_obj &>
% foreach my $group ( RT::CustomField->CustomGroupings( $asset_obj ), '' ) {
    <& /Asset/Elements/AssetSearchCFs, %ARGS, Grouping => $group, AssetObj => $asset_obj,
       CatalogObj => $catalog_obj &>
% }
<& /Elements/Submit, Label => loc('Search'), Name => 'SearchAssets' &>
</&>
</form>
