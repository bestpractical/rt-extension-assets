<& /Elements/Header, Title => loc("Create a new asset") &>
<& /Elements/Tabs &>

<& /Elements/ListActions, actions => \@results &>

<form method="post" enctype="multipart/form-data" id="CreateAsset">
  <input type="hidden" name="id" value="new">

  <&| /Widgets/TitleBox, title => "Basics", class => "asset-basics" &>
    <& Elements/EditBasics, %ARGS, AssetObj => $asset, ShowDisabled => 0 &>
  </&>

  <&| /Widgets/TitleBox, title => "Details", class => "asset-cfs" &>
    <!-- XXX TODO: use generic EditCustomFields element in core RT once it's ready -->
    <& Elements/EditCustomFields, %ARGS, Object => $asset &>
  </&>

  <!-- XXX TODO: People -->
  <!-- XXX TODO: Links -->

  <& /Elements/Submit, Label => loc('Create asset') &>
</form>

<%args>
$id => ""
</%args>
<%init>
my $asset = RT::Asset->new( $session{CurrentUser} );

Abort(loc("You don't have permission to create assets."))
    unless $asset->CurrentUserHasRight("CreateAsset");

my @results;

if ($id eq "new") {
    my $skip_create = 0;

    my ($cf_ok, @cf_errors) = $m->comp(
        '/Elements/ValidateCustomFields',
        CustomFields    => $asset->CustomFields,
        NamePrefix      => "Object-RT::Asset--CustomField-",
        ARGSRef         => \%ARGS
    );

    $m->callback(
        CallbackName    => 'BeforeCreate',
        AssetObj        => $asset,
        results         => \@results,
        cf_ok           => \$cf_ok,
        skip_create     => \$skip_create,
        ARGSRef         => \%ARGS
    );

    if ($cf_ok and not $skip_create) {
        # Handle CFs
        my %create = ProcessObjectCustomFieldUpdatesForCreate( ARGSRef => \%ARGS );

        # Handle basic fields
        for ($asset->WritableAttributes) {
            $create{$_} = $ARGS{$_} if exists $ARGS{$_};
        }

        $m->callback( %ARGS, AssetObj => $asset, Create => \%create, CallbackName => 'MassageCreate' );

        my ($ok, $msg) = $asset->Create( %create );
        if ($ok) {
            MaybeRedirectForResults(
                Actions     => [$msg],
                Path        => "/Asset/Display.html",
                Arguments   => { id => $asset->id },
            );
        } else {
            # Report the error but don't redirect away so they can re-submit
            push @results, $msg;
        }
    } else {
        push @results, @cf_errors;
    }
}
</%init>
