<& /Elements/Header, Title => loc("Create a new asset in catalog [_1]", $catalog->Name),
    RT->Config->Get('AssetBasicCustomFieldsOnCreate')
      ? (
        onload => "function () { hide('Asset-Create-details') }"
      )
      : (),
 &>
<& /Elements/Tabs &>

<& /Elements/ListActions, actions => \@results &>

<form method="post" enctype="multipart/form-data" id="CreateAsset" action="Create.html">
  <input type="hidden" name="id" value="new">
  <input type="hidden" name="Catalog" value="<% $catalog->id %>">

<div id="Asset-Create-basics">
<a name="basics"></a>

  <&| /Widgets/TitleBox, title => loc("Basics"), class => "asset-basics" &>
    <& Elements/EditBasics, %ARGS, AssetObj => $asset, CatalogObj => $catalog &>
  </&>

  <&| /Widgets/TitleBox, title => loc("People"), class => "asset-people" &>
    <& Elements/EditPeople, %ARGS, AssetObj => $asset &>
  </&>

</div>

<div id="Asset-Create-details">
<a name="details"></a>
  <&| /Widgets/TitleBox, title => loc("Links"), class => "asset-links" &>
    <& /Elements/AddLinks,
        Object          => $asset,
        CustomFields    => $catalog->AssetCustomFields &>
  </&>

  <& /Elements/EditCustomFieldCustomGroupings,
        Object => $asset,
        CustomFieldGenerator => sub { $catalog->AssetCustomFields } &>
</div>

  <& /Elements/Submit, Label => loc('Create asset') &>
</form>

<%args>
$id => ""
$Catalog => undef
</%args>
<%init>
my $asset   = RT::Asset->new( $session{CurrentUser} );
my $catalog = RT::Catalog->new( $session{CurrentUser} );
$catalog->Load($Catalog);

Abort(loc("Unable to find catalog '[_1]'", $Catalog))
    unless $catalog->id;

Abort(loc("You don't have permission to create assets in catalog [_1].",
          $catalog->Name || $catalog->id))
    unless $catalog->CurrentUserHasRight("CreateAsset");

# Update the current default with the latest selection
$session{'DefaultCatalog'} = $catalog->Id;

my @results;

if ($id eq "new") {
    my $skip_create = 0;

    my ($cf_ok, @cf_errors) = $m->comp(
        '/Elements/ValidateCustomFields',
        Object          => $asset,
        CustomFields    => $catalog->AssetCustomFields,
        ARGSRef         => \%ARGS
    );

    $m->callback(
        CallbackName    => 'BeforeCreate',
        AssetObj        => $asset,
        CatalogObj      => $catalog,
        results         => \@results,
        cf_ok           => \$cf_ok,
        skip_create     => \$skip_create,
        ARGSRef         => \%ARGS
    );

    if ($cf_ok and not $skip_create) {
        # Handle CFs and links
        my %create = (
            ProcessObjectCustomFieldUpdatesForCreate( ARGSRef => \%ARGS ),
            ProcessLinksForCreate( ARGSRef => \%ARGS ),
            map {
                $_ => $ARGS{$_}
            } $asset->Roles,
        );

        # Handle basic fields
        for ($asset->WritableAttributes) {
            $create{$_} = $ARGS{$_} if exists $ARGS{$_};
        }

        $m->callback( %ARGS, AssetObj => $asset, CatalogObj => $catalog, Create => \%create, CallbackName => 'MassageCreate' );

        my ($ok, $msg, $nonfatal) = $asset->Create( %create );
        push @results, $msg, @{$nonfatal || []};

        if ($ok) {
            MaybeRedirectForResults(
                Actions     => \@results,
                Path        => "/Asset/Display.html",
                Arguments   => { id => $asset->id },
            );
        }
    } else {
        push @results, @cf_errors;
    }
}

if ( RT->Config->Get('AssetBasicCustomFieldsOnCreate') ) {
    PageMenu->child( basics => raw_html =>  q[<a href="#basics" onclick="return switchVisibility('Asset-Create-basics','Asset-Create-details');">] . loc('Basics') . q[</a>]);
    PageMenu->child( details => raw_html =>  q[<a href="#details" onclick="return switchVisibility('Asset-Create-details','Asset-Create-basics');">] . loc('Details') . q[</a>]);
}

</%init>
