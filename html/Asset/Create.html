<& /Elements/Header, Title => loc("Create a new asset") &>
<& /Elements/Tabs &>

<& /Elements/ListActions, actions => \@results &>

<form method="post" enctype="multipart/form-data" id="CreateAsset" action="Create.html">
  <input type="hidden" name="id" value="new">

  <&| /Widgets/TitleBox, title => loc("Basics"), class => "asset-basics" &>
    <& Elements/EditBasics, %ARGS, AssetObj => $asset, ShowDisabled => 0 &>
  </&>

  <& /Elements/EditCustomFieldCustomGroupings, Object => $asset &>

  <!-- XXX TODO: People -->
  <!-- XXX TODO: Links -->

  <& /Elements/Submit, Label => loc('Create asset') &>
</form>

<%args>
$id => ""
</%args>
<%init>
my $asset = RT::Asset->new( $session{CurrentUser} );

Abort(loc("You don't have permission to create assets."))
    unless $asset->CurrentUserHasRight("CreateAsset");

my @results;

if ($id eq "new") {
    my $skip_create = 0;

    my ($cf_ok, @cf_errors) = $m->comp(
        '/Elements/ValidateCustomFields',
        Object          => $asset,
        CustomFields    => $asset->CustomFields,
        ARGSRef         => \%ARGS
    );

    $m->callback(
        CallbackName    => 'BeforeCreate',
        AssetObj        => $asset,
        results         => \@results,
        cf_ok           => \$cf_ok,
        skip_create     => \$skip_create,
        ARGSRef         => \%ARGS
    );

    if ($cf_ok and not $skip_create) {
        # Handle CFs
        my %create = ProcessObjectCustomFieldUpdatesForCreate( ARGSRef => \%ARGS );

        # Handle basic fields
        for ($asset->WritableAttributes) {
            $create{$_} = $ARGS{$_} if exists $ARGS{$_};
        }

        $m->callback( %ARGS, AssetObj => $asset, Create => \%create, CallbackName => 'MassageCreate' );

        my ($ok, $msg, $nonfatal) = $asset->Create( %create );
        push @results, $msg, @$nonfatal;

        if ($ok) {
            MaybeRedirectForResults(
                Actions     => \@results,
                Path        => "/Asset/Display.html",
                Arguments   => { id => $asset->id },
            );
        }
    } else {
        push @results, @cf_errors;
    }
}
</%init>
