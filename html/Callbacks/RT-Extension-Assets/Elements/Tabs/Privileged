<%init>
# Top level Assets menu
my $assets = Menu->child("tools")->add_before(
               "assets",        title => loc("Assets"),         path => "/Asset/");         # XXX TODO: link top level to search?
$assets->child("create",        title => loc("Create"),         path => "Create.html");
$assets->child("search",        title => loc("Search"));                                    # XXX TODO: link up to search

# Add global Assets custom field admin page
my $global_cfs = Menu();
   $global_cfs = $global_cfs->child($_) or last
        for "tools" => "config" => "global" => "custom-fields";
$global_cfs->child("assets", title => loc("Assets"), path => "/Admin/Global/CustomFields/Assets.html")
    if $global_cfs;

# Page menus
my $page    = PageMenu();
my $request = $r->path_info;
   $request =~ s{/+}{/}g;

my $emit_js;

if ($request =~ m{^/Asset/} and $DECODED_ARGS->{id} and $DECODED_ARGS->{id} !~ /\D/) {
    my $id    = $DECODED_ARGS->{id};
    my $asset = RT::Asset->new( $session{CurrentUser} );
    $asset->Load($id);

    $page->child("display",     title => loc("Display"),        path => "/Asset/Display.html?id=$id");
    $page->child("history",     title => loc("History"),        path => "/Asset/History.html?id=$id");
    $page->child("basics",      title => loc("Basics"),         path => "/Asset/Modify.html?id=$id");
    $page->child("links",       title => loc("Links"),          path => "/Asset/ModifyLinks.html?id=$id");
    $page->child("people",      title => loc("People"),         path => "/Asset/ModifyPeople.html?id=$id");
    $page->child("dates",       title => loc("Dates"),          path => "/Asset/ModifyDates.html?id=$id");

    for my $grouping (RT::CustomField->CustomGroupings($asset)) {
        $page->child(
            "cf-grouping-$grouping",
            title   => loc($grouping),
            path    => "/Asset/ModifyCFs.html?id=$id;Grouping=" . $m->interp->apply_escapes($grouping, 'u'),
        );
    }

    $emit_js = 1;
    $page->child("create-linked-ticket", title => loc("Create linked ticket"), path => "/Asset/CreateLinkedTicket.html?id=$id");
}
elsif ($request =~ m{^/Admin/Global/CustomFields/Assets\.html$}) {
    $page->child("create", title => loc("Create New"), path => "/Admin/CustomFields/Modify.html?Create=1;LookupType=RT::Asset");
}

return unless $emit_js;
</%init>
<script type="text/javascript">
jQuery(function(){
    jQuery("#page-create-linked-ticket").click(function(ev){
        ev.preventDefault();
        jQuery.get(
            RT.Config.WebPath + "/Asset/Helpers/CreateLinkedTicket?Asset=" + <% $DECODED_ARGS->{id} || 0 |n,j %>,
            function(html) {
                jQuery("<div class='modal'></div>")
                    .append(html).appendTo("body")
                    .bind('modal:close', function(ev,modal) { modal.elm.remove(); })
                    .modal();
            }
        );
    });
});
</script>
