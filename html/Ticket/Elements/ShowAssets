<%args>
$Ticket
$ShowRelatedTickets => 10
</%args>
<%init>
my $linked_assets = $Ticket->Links("Base")->Clone;
$linked_assets->Limit(
    FIELD       => "Target",
    OPERATOR    => "STARTSWITH",
    VALUE       => RT::URI::asset->LocalURIPrefix,
);

my @linked_assets = grep { defined }
                     map { $_->TargetURI->IsLocal }         # Returns the local asset ID
                        @{ $linked_assets->ItemsArrayRef };

my $asset_queue;
if (RT->Config->Get('AssetQueues')) {
    $asset_queue = 1 if grep {$_ eq $Ticket->QueueObj->Name} @{RT->Config->Get('AssetQueues')}
} else {
    $asset_queue = 1;
}
return unless @linked_assets or ($Ticket->CurrentUserHasRight("ModifyTicket")
                                     and $asset_queue);

my $assets = RT::Assets->new( $session{CurrentUser} );
$assets->OrderBy( FIELD => "Name", ORDER => "ASC" );
if ( @linked_assets ) {
    $assets->Limit(
        FIELD       => "id",
        OPERATOR    => "IN",
        VALUE       => \@linked_assets,
    );
}

my $Format = RT->Config->Get("AssetSummaryFormat") || q[
    '<a href="__WebHomePath__/Asset/Display.html?id=__id__">__Name__</a>/TITLE:Name',
    Description,
    Status,
    Catalog,
];

$m->callback(
    CallbackName    => 'ModifyCollection',
    Ticket          => $Ticket,
    Assets          => $assets,
    Format          => \$Format,
);


</%init>
<&| /Widgets/TitleBox,
    title => loc('Assets'),
    class => 'ticket-assets'
    &>

<form action="<% RT->Config->Get("WebPath") %>/Ticket/Display.html" method="POST" enctype="multipart/form-data">
  <input type="hidden" name="id" value="<% $Ticket->id %>">

% $m->callback( CallbackName => "Start", Ticket => $Ticket, Assets => $assets );

<div id="assets-accordion" class="rt-accordion">
% while (my $asset = $assets->Next) {
  <h3><a href="<% RT->Config->Get('WebPath') %>/Asset/Display.html?id=<% $asset->id %>"><&|/l, $asset->id, $asset->Name &>#[_1]: [_2]</&></a>
<%perl>
if ($Ticket->CurrentUserHasRight("ModifyTicket")) {
    my $links = $asset->Links("Target")->Clone;
    $links->Limit(
        FIELD   => "LocalBase",
        VALUE   => $Ticket->id,
    );

    my %params = map { join("-", "DeleteLink", "", $_->Type, $_->Target) => 1 }
                    @{ $links->ItemsArrayRef };

    my $delete_url = RT->Config->Get("WebPath")
        . "/Ticket/Display.html?"
        . $m->comp("/Elements/QueryString", id => $Ticket->id, %params);
</%perl>
<a href="<% $delete_url %>" class="unlink-asset ui-icon ui-icon-circle-close" title="Unlink asset">X</a>
% }
  </h3>
  <div class="details">
    <& /Elements/ShowRecord,
        Object      => $asset,
        Format      => $Format,
        TrustFormat => 1,
        &>

<%perl>
if ($ShowRelatedTickets) {
    my %search = (
        Query   => "id != '@{[$Ticket->id]}' AND LinkedTo = 'asset:@{[$asset->id]}'",
        OrderBy => "LastUpdated",
        Order   => "DESC",
    );
    my $url = RT->Config->Get("WebPath")
            . "/Search/Results.html?"
            . $m->comp("/Elements/QueryString", %search);
</%perl>
    <div class="related-tickets">
      <span class="label">
        <a href="<% $url %>">
          <&|/l, $ShowRelatedTickets &>[_1] most recently updated related tickets</&>
        </a>
      </span>
      <& /Elements/CollectionList,
          %search,
          Class             => "RT::Tickets",
          Format            => RT->Config->Get("AssetSummaryRelatedTicketsFormat"),
          Rows              => $ShowRelatedTickets,
          ShowHeader        => 0,
          AllowSorting      => 0,
          ShowNavigation    => 0,
      &>
    </div>
% }

% $m->callback( CallbackName => "PerAsset", Ticket => $Ticket, Asset => $asset );

  </div>
% }
</div>

  <div class="add-asset">
    <label>
      <&|/l&>Add an asset to this ticket</&>
    <input size="5" name="<% $Ticket->id %>-RefersTo" placeholder="<&|/l&>Asset #</&>" type="text">
    </label>
    <input type="submit" value="+">
  </div>

% $m->callback( CallbackName => "End", Ticket => $Ticket, Assets => $assets );

</form>

</&>
