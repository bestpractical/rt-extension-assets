<%args>
$QueueObj
$ARGSRef
</%args>
<%init>
my @linked_assets;
foreach my $key ( map {+("new-$_", "$_-new")} keys %RT::Link::DIRMAP ) {
    next unless $ARGSRef->{$key};
    for my $linktext (grep $_, split ' ', $ARGSRef->{$key}) {
        my $uri = RT::URI->new( $session{'CurrentUser'} );
        next unless $uri->FromURI( $linktext );
        next unless $uri->IsLocal and $uri->Object and $uri->Object->id and $uri->Object->isa("RT::Asset");
        push @linked_assets, $uri->Object->id;
    }
}

my $asset_queue;
if (RT->Config->Get('AssetQueues')) {
    $asset_queue = 1 if grep {$_ eq $QueueObj->Name} @{RT->Config->Get('AssetQueues')}
} else {
    $asset_queue = 1;
}

return unless @linked_assets or $asset_queue;

my $assets = RT::Assets->new( $session{CurrentUser} );
$assets->OrderBy( FIELD => "Name", ORDER => "ASC" );
if ( @linked_assets ) {
    $assets->Limit(
        FIELD       => "id",
        OPERATOR    => "IN",
        VALUE       => \@linked_assets,
    );
}

my $Format = RT->Config->Get("AssetSummaryFormat") || q[
    '<a href="__WebHomePath__/Asset/Display.html?id=__id__">__Name__</a>/TITLE:Name',
    Description,
    Status,
    Catalog,
];

$m->callback(
    CallbackName    => 'ModifyCollection',
    Queue           => $QueueObj,
    Assets          => $assets,
    Format          => \$Format,
);


</%init>
<&| /Widgets/TitleBox,
    title => loc('Assets'),
    class => 'ticket-assets'
    &>

% $m->callback( CallbackName => "Start", Queue => $QueueObj, Assets => $assets );

<div id="assets-accordion" class="rt-accordion">
% while (my $asset = $assets->Next) {
  <h3><a href="<% RT->Config->Get('WebPath') %>/Asset/Display.html?id=<% $asset->id %>"><&|/l, $asset->id, $asset->Name &>#[_1]: [_2]</&></a></h3>
  <div class="details">
    <& /Elements/ShowRecord,
        Object      => $asset,
        Format      => $Format,
        TrustFormat => 1,
        &>
% $m->callback( CallbackName => "PerAsset", Queue => $QueueObj, Asset => $asset );
  </div>
% }
</div>

% $m->callback( CallbackName => "End", Queue => $QueueObj, Assets => $assets );

</form>

</&>
